module

public import Foundation.Modal.Logic.GL.Independency
public import Foundation.Modal.Logic.S.Basic
public import Mathlib.Order.WellFoundedSet

@[expose] public section

namespace LO.Modal

open Formula (atom)
open Logic

protected abbrev D := sumQuasiNormal Modal.GL {âˆ¼â–¡âŠ¥, â–¡(â–¡(atom 0) â‹ â–¡(.atom 1)) â â–¡(atom 0) â‹ â–¡(.atom 1)}
instance : Modal.D.IsQuasiNormal := inferInstance

instance : Entailment.HasAxiomP Modal.D where
  P := by
    constructor;
    apply Logic.sumQuasiNormal.memâ‚‚;
    apply Logic.iff_provable.mpr;
    simp;

lemma D.mem_axiomDz : Modal.D âŠ¢ â–¡(â–¡Ï† â‹ â–¡Ïˆ) â â–¡Ï† â‹ â–¡Ïˆ := by
  apply Logic.subst (Ï† := â–¡(â–¡(atom 0) â‹ â–¡(.atom 1)) â â–¡(atom 0) â‹ â–¡(.atom 1)) (s := Î» a => if a = 0 then Ï† else Ïˆ);
  apply Logic.sumQuasiNormal.memâ‚‚!;
  apply Logic.iff_provable.mpr;
  simp;

instance : Modal.GL âª± Modal.D := by
  constructor;
  . infer_instance;
  . apply Entailment.not_weakerThan_iff.mpr;
    use âˆ¼â–¡âŠ¥;
    constructor;
    . simp;
    . simpa using GL.unprovable_notbox;

section

private inductive D' : Logic â„•
  | mem_GL {Ï†} : Modal.GL âŠ¢ Ï† â†’ Modal.D' Ï†
  | axiomP : Modal.D' (âˆ¼â–¡âŠ¥)
  | axiomD (Ï† Ïˆ) : Modal.D' (â–¡(â–¡Ï† â‹ â–¡Ïˆ) â â–¡Ï† â‹ â–¡Ïˆ)
  | mdp  {Ï† Ïˆ} : Modal.D' (Ï† â Ïˆ) â†’ Modal.D' Ï† â†’ Modal.D' Ïˆ

private lemma D'.eq_D : Modal.D' = Modal.D := by
  ext Ï†;
  constructor;
  . intro h;
    apply iff_provable.mp;
    induction h with
    | mem_GL h => exact sumQuasiNormal.memâ‚! h;
    | mdp _ _ ihÏ†Ïˆ ihÏ† => exact ihÏ†Ïˆ â¨€ ihÏ†;
    | axiomD Ï† => apply Modal.D.mem_axiomDz;
    | axiomP => simp;
  . intro h;
    induction h with
    | memâ‚ h => exact Modal.D'.mem_GL h;
    | memâ‚‚ h =>
      rcases h with (rfl | rfl);
      . apply Modal.D'.axiomP;
      . apply Modal.D'.axiomD;
    | mdp _ _ ihÏ†Ïˆ ihÏ† => exact Modal.D'.mdp ihÏ†Ïˆ ihÏ†;
    | subst hÏ† ihÏ† =>
      clear hÏ†;
      induction ihÏ† with
      | mem_GL h =>
        apply Modal.D'.mem_GL;
        apply Logic.subst;
        exact h;
      | axiomP => apply Modal.D'.axiomP;
      | axiomD _ _ => apply Modal.D'.axiomD;
      | mdp _ _ ihÏ†Ïˆ ihÏ† => apply Modal.D'.mdp ihÏ†Ïˆ ihÏ†;

-- TODO: Remove `eq_D_D'`?
protected def D.rec'
  {motive  : (Ï† : Formula â„•) â†’ (Modal.D âŠ¢ Ï†) â†’ Prop}
  (mem_GL  : âˆ€ {Ï†}, (h : Modal.GL âŠ¢ Ï†) â†’ motive Ï† (sumQuasiNormal.memâ‚! h))
  (axiomP  : motive (âˆ¼â–¡âŠ¥) (by simp))
  (axiomDz : âˆ€ {Ï† Ïˆ}, motive (â–¡(â–¡Ï† â‹ â–¡Ïˆ) â â–¡Ï† â‹ â–¡Ïˆ) (Modal.D.mem_axiomDz))
  (mdp : âˆ€ {Ï† Ïˆ}, {hÏ†Ïˆ : Modal.D âŠ¢ Ï† â Ïˆ} â†’ {hÏ† : Modal.D âŠ¢ Ï†} â†’ (motive (Ï† â Ïˆ) hÏ†Ïˆ) â†’ (motive Ï† hÏ†) â†’ motive Ïˆ (hÏ†Ïˆ â¨€ hÏ†))
  : âˆ€ {Ï†}, (h : Modal.D âŠ¢ Ï†) â†’ motive Ï† h := by
  intro Ï† h;
  replace h := iff_provable.mp $ Modal.D'.eq_D â–¸ h;
  induction h with
  | mem_GL h => apply mem_GL; assumption;
  | axiomP => apply axiomP;
  | axiomD Ï† Ïˆ => apply axiomDz;
  | mdp hÏ†Ïˆ hÏ† ihÏ†Ïˆ ihÏ† =>
    apply mdp;
    . apply ihÏ†Ïˆ;
    . apply ihÏ†;
    . replace hÏ†Ïˆ := iff_provable.mpr hÏ†Ïˆ;
      rwa [D'.eq_D] at hÏ†Ïˆ;
    . replace hÏ† := iff_provable.mpr hÏ†;
      rwa [D'.eq_D] at hÏ†;

end


section

open LO.Entailment LO.Modal.Entailment

@[simp]
lemma GL.box_disj_Tc {l : List (Formula â„•)} : Modal.GL âŠ¢ (â–¡'l).disj â â–¡(â–¡'l).disj := by
  apply left_Disj!_intro;
  intro Ïˆ hÏˆ;
  obtain âŸ¨Ïˆ, hÏˆ, rflâŸ© := List.LO.exists_of_mem_box hÏˆ;
  apply C!_trans axiomFour!;
  apply axiomK'!;
  apply nec!;
  apply right_Disj!_intro;
  assumption;

lemma D.ldisj_axiomDz {l : List (Formula â„•)} : Modal.D âŠ¢ â–¡((â–¡'l).disj) â (â–¡'l).disj := by
  induction l with
  | nil => exact axiomP!;
  | cons Ï† l ih =>
    apply C!_replace ?_ ?_ (D.mem_axiomDz (Ï† := Ï†) (Ïˆ := (â–¡'l).disj));
    . apply sumQuasiNormal.memâ‚!;
      apply axiomK'!;
      apply nec!;
      suffices Modal.GL âŠ¢ â–¡Ï† â‹ (â–¡'l).disj â â–¡Ï† â‹ â–¡(â–¡'l).disj by simpa;
      have : Modal.GL âŠ¢ (â–¡'l).disj â â–¡(â–¡'l).disj := GL.box_disj_Tc;
      cl_prover [this];
    . suffices Modal.D âŠ¢ â–¡Ï† â‹ â–¡(â–¡'l).disj â â–¡Ï† â‹ (â–¡'l).disj by simpa;
      cl_prover [ih];

lemma D.fdisj_axiomDz {s : Finset (Formula â„•)} : Modal.D âŠ¢ â–¡((â–¡'s).disj) â (â–¡'s).disj := by
  apply C!_replace ?_ ?_ $ D.ldisj_axiomDz (l := s.toList);
  . apply sumQuasiNormal.memâ‚!;
    apply axiomK'!;
    apply nec!;
    apply left_Fdisj!_intro;
    rintro Ïˆ hÏˆ;
    apply right_Disj!_intro;
    obtain âŸ¨Ïˆ, hÏˆ, rflâŸ© : âˆƒ a âˆˆ s, â–¡a = Ïˆ := Finset.LO.exists_of_mem_box hÏˆ;
    grind;
  . apply left_Disj!_intro;
    intro Ïˆ hÏˆ;
    apply right_Fdisj!_intro;
    obtain âŸ¨Ïˆ, hÏˆâ‚‚, rflâŸ© := List.LO.exists_of_mem_box hÏˆ;
    grind;

lemma D.axiomFour : Modal.D âŠ¢ â–¡â–¡Ï† â â–¡Ï† := by
  simpa [Finset.LO.preboxItr, Finset.LO.boxItr] using fdisj_axiomDz (s := {Ï†});

noncomputable abbrev Formula.dzSubformula (Ï† : Formula â„•) := (â–¡â»Â¹'Ï†.subformulas).powerset.image (Î» s => â–¡((â–¡'s).disj) â (â–¡'s).disj)


namespace Kripke

instance {F : Frame} [F.IsFinite] [F.IsIrreflexive] [F.IsTransitive] : F.IsConverseWellFounded := âŸ¨by
  apply Finite.converseWellFounded_of_trans_irrefl';
  . infer_instance;
  . intro x y z; apply F.trans;
  . intro x; apply F.irrefl;
âŸ©

namespace Model

/--
  `Ï‰`-extend root model.
  Valuation on `n âˆˆ Ï‰` is same on `M.root` and on point `Ï‰` is by `o`.
-/
abbrev tailModelâ‚€ (M : Kripke.Model) [M.IsPointRooted] (o : â„• â†’ Prop) : Kripke.Model where
  World := Unit âŠ• â„• âŠ• M.World -- `Unit` means `Ï‰`
  Rel x y :=
    match x, y with
    | _            , .inl _        => False -- Â¬(x â‰º Ï‰)
    | .inl        _, _             => True  -- Ï‰ â‰º x where x is not Ï‰
    | .inr $ .inl x, .inr $ .inl y => x > y -- x â‰º y â†” x > y where x, y âˆˆ Ï‰
    | .inr $ .inl _, .inr $ .inr _ => True
    | .inr $ .inr _, .inr $ .inl _ => False
    | .inr $ .inr x, .inr $ .inr y => x â‰º y
  Val p x :=
    match x with
    | .inl _        => o p
    | .inr $ .inl _ => M.Val p M.root.1
    | .inr $ .inr x => M.Val p x

namespace tailModelâ‚€

variable {M : Kripke.Model} [M.IsPointRooted] {o}

instance instPontRooted : (M.tailModelâ‚€ o).IsPointRooted where
  default := âŸ¨.inl (), by grindâŸ©
  uniq {r'} := by
    by_contra! hC;
    have := r'.2 (.inl ()) (by grind);
    simp [Frame.Rel'] at this;

instance instTransitive [M.IsTransitive] : (M.tailModelâ‚€ o).IsTransitive := âŸ¨by grindâŸ©

@[coe] abbrev embed_nat (n : â„•) : M.tailModelâ‚€ o := .inr $ .inl n

@[coe] abbrev embed_original (x : M) : M.tailModelâ‚€ o := .inr $ .inr x

instance instCWF [M.IsConverseWellFounded] : (M.tailModelâ‚€ o).IsConverseWellFounded := âŸ¨by
  apply ConverseWellFounded.iff_has_max.mpr;
  intro s hs;
  let sâ‚ := { x | (Sum.inr $ Sum.inr x) âˆˆ s };
  let sâ‚‚ := { x | (Sum.inr $ Sum.inl x) âˆˆ s };
  by_cases hsâ‚ : sâ‚.Nonempty;
  . obtain âŸ¨m, hmâ‚, hmâ‚‚âŸ© := ConverseWellFounded.iff_has_max.mp M.cwf sâ‚ (by simpa);
    use embed_original m;
    grind;
  . by_cases hsâ‚‚ : sâ‚‚.Nonempty;
    . let m := Set.IsWF.min (s := sâ‚‚) (Set.IsWF.of_wellFoundedLT _) (by assumption);
      use embed_nat m;
      constructor;
      . simpa using Set.IsWF.min_mem (s := sâ‚‚) _ _;
      . intro x hx;
        match x with
        | .inl _ => grind;
        | .inr $ .inr x =>
          exfalso;
          apply hsâ‚;
          use x;
          grind;
        | .inr $ .inl n =>
          suffices m â‰¤ n by grind;
          apply Set.IsWF.min_le;
          grind;
    . use (M.tailModelâ‚€ o).root;
      simp [Set.Nonempty] at hsâ‚ hsâ‚‚;
      constructor;
      . contrapose! hs;
        ext x;
        match x with | .inl _ | .inr $ .inl n | .inr $ .inr x => tauto;
      . simp_all [tailModelâ‚€, sâ‚, sâ‚‚];
âŸ©

protected def pMorphism_original : M â†’â‚š (M.tailModelâ‚€ o) where
  toFun := embed_original
  forth := by grind;
  back := by grind;
  atomic := by grind;

lemma modal_equivalent_original {x : M} : ModalEquivalent (Mâ‚ := M) (Mâ‚‚ := M.tailModelâ‚€ o) x (embed_original x) := by
  apply tailModelâ‚€.pMorphism_original.modal_equivalence;

open Formula.Kripke

lemma satisfies_box_of_satisfies_box_at_root [M.IsTransitive] (h : Satisfies _ (M.tailModelâ‚€ o).root.1 (â–¡Ï†)) {x : M.tailModelâ‚€ o} : Satisfies _ x (â–¡Ï†) := by
  intro y Rxy;
  apply h;
  dsimp [Frame.root, default];
  grind;

protected def pMorphism_extendRoot (M : Model) [M.IsPointRooted] (n) : (M.extendRoot n) â†’â‚š (M.tailModelâ‚€ o) where
  toFun := Î» x =>
    match x with
    | .inl i => embed_nat i
    | .inr x => embed_original x
  forth := by
    rintro (x | x) (y | y) Rxy <;>
    simp_all only [Model.extendRoot, Frame.extendRoot, tailModelâ‚€];
    case inl.inl => omega;
  back := by
    rintro (x | x) (y | y | y) Rxy;
    case inl.inr.inl => use Sum.inl âŸ¨y, by grindâŸ©; grind;
    all_goals simp_all [Frame.Rel', tailModelâ‚€, Model.extendRoot, Frame.extendRoot];
  atomic := by rintro a (w | w) <;> grind;

lemma modal_equivalent_extendRoot_original {n : â„•+} {x : M} : ModalEquivalent (Mâ‚ := M.extendRoot n) (Mâ‚‚ := M.tailModelâ‚€ o) x (embed_original x) := by
  apply tailModelâ‚€.pMorphism_extendRoot M n |>.modal_equivalence;

lemma modal_equivalent_extendRoot_nat {n : â„•+} {i : Fin n} : ModalEquivalent (Mâ‚ := M.extendRoot n) (Mâ‚‚ := M.tailModelâ‚€ o) (Sum.inl i) (embed_nat i) := by
  apply tailModelâ‚€.pMorphism_extendRoot M n |>.modal_equivalence;

open Formula.Kripke in
lemma of_provable_rflSubformula_original_root [M.IsTransitive]
  {Ï† : Formula _}
  (hS : M.root.1 âŠ§ ((â–¡â»Â¹'Ï†.subformulas).image (Î» Ïˆ => â–¡Ïˆ â Ïˆ)).conj) :
  âˆ€ Ïˆ âˆˆ Ï†.subformulas, âˆ€ i : â„•, M.root.1 âŠ§ Ïˆ â†” Satisfies (M.tailModelâ‚€ o) (embed_nat i) Ïˆ := by
  intro Ïˆ hÏˆ i;
  induction Ïˆ generalizing i with
  | hatom p | hfalsum => simp [Satisfies];
  | himp Ïˆ Î¾ ihÏˆ ihÎ¾ =>
    simp [ihÏˆ (by grind) i, ihÎ¾ (by grind) i, Satisfies];
  | hbox Ïˆ ihÏˆ =>
    replace ihÏˆ := ihÏˆ (by grind);
    calc
      _ â†” (âˆ€ x, M.root.1 â‰º x â†’ x âŠ§ Ïˆ) âˆ§ (M.root.1 âŠ§ Ïˆ) := by
        suffices (âˆ€ y, M.root.1 â‰º y â†’ y âŠ§ Ïˆ) â†’ M.root.1 âŠ§ Ïˆ by simpa [Satisfies];
        apply Satisfies.fconj_def.mp hS (â–¡Ïˆ â Ïˆ) $ by
          simp only [Finset.LO.preboxItr, Function.iterate_one, Finset.mem_image, Finset.mem_preimage];
          use Ïˆ;
      _ â†” (âˆ€ x : M, x âŠ§ Ïˆ) âˆ§ (âˆ€ j < i, Satisfies (M.tailModelâ‚€ o) (embed_nat j) Ïˆ) := by grind;
      _ â†” (âˆ€ x, Satisfies (M.tailModelâ‚€ o) (embed_original x) Ïˆ) âˆ§ (âˆ€ j < i, Satisfies (M.tailModelâ‚€ o) (embed_nat j) Ïˆ) := by
        apply and_congr_left';
        simp only [@modal_equivalent_original (M := M) (o := o) (Ï† := Ïˆ)];
        constructor;
        . intro h x;
          apply h;
        . intro h x;
          apply h;
      _ â†” _ := by
        constructor;
        . rintro âŸ¨hâ‚, hâ‚‚âŸ© (_ | j | y) R <;> grind;
        . rintro h;
          constructor;
          . intro x;
            apply h;
            grind;
          . intro j hj;
            apply h;
            grind;

end tailModelâ‚€


def tailModel (M : Kripke.Model) [M.IsPointRooted] : Kripke.Model := tailModelâ‚€ M (M Â· M.root.1)


namespace tailModel

variable {M : Kripke.Model} [M.IsPointRooted]

instance instPointRooted : (tailModel M).IsPointRooted := tailModelâ‚€.instPontRooted

end tailModel

end Model


end Kripke


section

open Classical
open Kripke
open Kripke.Model
open Formula.Kripke

theorem GL_D_TFAE :
  [
    Modal.D âŠ¢ Ï†,
    âˆ€ M : Kripke.Model, [M.IsFinite] â†’ [M.IsTransitive] â†’ [M.IsIrreflexive] â†’ [M.IsRooted] â†’ âˆ€ o, Satisfies _ (M.tailModelâ‚€ o).root.1 Ï†,
    âˆ€ M : Kripke.Model, [M.IsFinite] â†’ [M.IsTransitive] â†’ [M.IsIrreflexive] â†’ [M.IsRooted] â†’ M.root.1 âŠ§ Ï†.dzSubformula.conj â Ï†,
    Modal.GL âŠ¢ Ï†.dzSubformula.conj â Ï†,
  ].TFAE := by
    tfae_have 1 â†’ 2 := by
      intro h M _ _ _ _ o;
      induction h using D.rec' with
      | mem_GL h =>
        apply Sound.sound (ğ“œ := Kripke.FrameClass.GL) h;
        apply Set.mem_setOf_eq.mpr;
        exact {
          trans {x y z} := by apply (M.tailModelâ‚€ o).trans,
          cwf := by apply (M.tailModelâ‚€ o).cwf,
        }
      | axiomP =>
        apply Satisfies.not_def.mpr;
        apply Satisfies.not_box_def.mpr;
        use tailModelâ‚€.embed_original M.root;
        constructor;
        . grind;
        . tauto;
      | @axiomDz Ï† Ïˆ =>
        intro h;
        contrapose! h;
        replace h := Satisfies.or_def.not.mp h;
        push_neg at h;
        obtain âŸ¨x, Rrx, hxâŸ© := Satisfies.not_box_def.mp h.1;
        obtain âŸ¨y, Rry, hyâŸ© := Satisfies.not_box_def.mp h.2;

        apply Satisfies.not_box_def.mpr;
        let z : M.tailModelâ‚€ o := tailModelâ‚€.embed_nat $
          match x, y with
          | .inr $ .inl x, .inr $ .inl y => (max x y) + 1
          | .inr $ .inr _, .inr $ .inl y => y + 1
          | .inr $ .inl x, .inr $ .inr _ => x + 1
          | .inr $ .inr x, .inr $ .inr y => 0
        have Rzx : z â‰º x := by
          unfold z;
          match x, y with
          | .inr $ .inl _, .inr $ .inl _ => dsimp [tailModelâ‚€]; omega;
          | .inr $ .inr _, .inr $ .inl _
          | .inr $ .inl _, .inr $ .inr _
          | .inr $ .inr _, .inr $ .inr _ => grind;
        have Rzy : z â‰º y := by
          unfold z;
          match x, y with
          | .inr $ .inl _, .inr $ .inl _ => dsimp [tailModelâ‚€]; omega;
          | .inr $ .inr _, .inr $ .inl _
          | .inr $ .inl _, .inr $ .inr _
          | .inr $ .inr _, .inr $ .inr _ => grind;
        use z;
        constructor;
        . grind;
        . apply Satisfies.or_def.not.mpr;
          push_neg;
          constructor;
          . apply Satisfies.not_box_def.mpr;
            use x;
          . apply Satisfies.not_box_def.mpr;
            use y;
      | mdp ihÏ†Ïˆ ihÏ† => exact ihÏ†Ïˆ ihÏ†
    tfae_have 2 â†’ 3 := by
      contrapose!;
      rintro âŸ¨M, _, _, _, _, hâŸ©;
      have hâ‚ : âˆ€ X âŠ† (â–¡â»Â¹'Ï†.subformulas), Satisfies M M.root (â–¡(â–¡'X).disj â (â–¡'X).disj) := by simpa using Satisfies.not_imp_def.mp h |>.1;
      have hâ‚‚ := Satisfies.not_imp_def.mp h |>.2;

      let X := (â–¡â»Â¹'Ï†.subformulas).filter (Î» Ïˆ => Â¬(M.root.1 âŠ§ â–¡Ïˆ));
      obtain âŸ¨x, Rrx, hxâŸ© : âˆƒ x, M.root.1 â‰º x âˆ§ âˆ€ Ïˆ âˆˆ X, Â¬x âŠ§ â–¡Ïˆ := by
        have : M.root.1 âŠ§ âˆ¼((â–¡'X).disj) := by
          apply Satisfies.not_def.mpr;
          apply Satisfies.fdisj_def.not.mpr;
          simp [X, Finset.LO.preboxItr, Finset.LO.boxItr];
        have : M.root.1 âŠ§ âˆ¼â–¡((â–¡'X).disj) := by
          have := hâ‚ X $ by simp [X];
          tauto;
        obtain âŸ¨x, Rrx, hxâŸ© := Satisfies.not_box_def.mp this;
        use x;
        constructor;
        . assumption;
        . simpa [Finset.LO.preboxItr, Finset.LO.boxItr] using Satisfies.fdisj_def.not.mp hx;

      refine âŸ¨(Mâ†¾x), inferInstance, inferInstance, inferInstance, inferInstance, (M.Val Â· M.root), ?_âŸ©;
      exact (show âˆ€ Ïˆ âˆˆ Ï†.subformulas, Satisfies _ _ Ïˆ â†” Satisfies M M.root Ïˆ by
        intro Ïˆ hÏˆ;
        induction Ïˆ with
        | hatom p | hfalsum => simp [Satisfies];
        | himp Ï† Ïˆ ihÏ† ihÏˆ => simp [Satisfies, ihÏ† (by grind), ihÏˆ (by grind)];
        | hbox Ïˆ ihÏˆ =>
          replace ihÏˆ := ihÏˆ (by grind);
          constructor;
          . intro h;
            have : x âŠ§ â–¡Ïˆ :=
              Model.pointGenerate.modal_equivalent _ _ |>.mp
              $ tailModelâ‚€.modal_equivalent_original |>.mpr
              $ tailModelâ‚€.satisfies_box_of_satisfies_box_at_root (x := tailModelâ‚€.embed_original âŸ¨x, by grindâŸ©) h
            contrapose! this;
            apply hx;
            simp_all [X, Finset.LO.preboxItr, Finset.LO.boxItr];
          . intro h w Rrw;
            have Hâ‚ : âˆ€ w : Mâ†¾x, Satisfies _ w Ïˆ := by
              intro w;
              apply Model.pointGenerate.modal_equivalent x w |>.mpr;
              apply h;
              grind;
            match w with
            | .inl _ => contradiction;
            | .inr $ .inr w => exact tailModelâ‚€.modal_equivalent_original.mp $ Hâ‚ w;
            | .inr $ .inl n =>
              apply tailModelâ‚€.of_provable_rflSubformula_original_root (M := Mâ†¾x) (Ï† := Ï†) ?_ Ïˆ (by grind) n |>.mp;
              . apply Hâ‚;
              . apply Model.pointGenerate.modal_equivalent_at_root x |>.mpr;
                apply Satisfies.conj_def.mpr;
                suffices âˆ€ (Ïˆ : Formula â„•), â–¡Ïˆ âˆˆ Ï†.subformulas â†’ x âŠ§ (â–¡Ïˆ â Ïˆ) by simpa [Finset.LO.preboxItr, Finset.LO.boxItr];
                intro Ïˆ _ hÏˆ;
                have : Ïˆ âˆ‰ X := by
                  contrapose! hÏˆ;
                  apply hx;
                  assumption;
                have : M.root.1 âŠ§ (â–¡Ïˆ) := by
                  simp [X, Finset.LO.preboxItr] at this;
                  tauto;
                apply this;
                assumption;
      ) Ï† (show Ï† âˆˆ Ï†.subformulas by grind) |>.not.mpr $ hâ‚‚;
    tfae_have 4 â†” 3 := GL.Kripke.finite_completeness_TFAE.out 0 3;
    tfae_have 4 â†’ 1 := by
      intro h;
      apply (show Modal.D âŠ¢ Ï†.dzSubformula.conj â Ï† by exact sumQuasiNormal.memâ‚! h) â¨€ ?_;
      apply FConj!_iff_forall_provable.mpr;
      intro Ïˆ hÏˆ;
      obtain âŸ¨s, _, rflâŸ© : âˆƒ s âŠ† (â–¡â»Â¹'Ï†.subformulas), â–¡(â–¡'s).disj â (â–¡'s).disj = Ïˆ := by simpa using hÏˆ;
      exact D.fdisj_axiomDz;
    tfae_finish;

lemma iff_provable_D_provable_GL : Modal.D âŠ¢ Ï† â†” Modal.GL âŠ¢ Ï†.dzSubformula.conj â Ï† := GL_D_TFAE (Ï† := Ï†) |>.out 0 3

lemma D.unprovable_T : Modal.D âŠ¬ (Axioms.T (.atom 0)) := by
  apply GL_D_TFAE |>.out 0 1 |>.not.mpr;
  push_neg;
  let M : Kripke.Model := {
    World := Fin 1,
    Rel := (Â· < Â·),
    Val := Î» p i => True,
  }
  use M;
  refine âŸ¨inferInstance, inferInstance, inferInstance, âŸ¨0, by grindâŸ©, Î» _ => False, ?_âŸ©;
  . apply Satisfies.not_imp_def.mpr
    constructor;
    . intro x Rrx;
      match x with
      | .inr $ .inl x => simp [Satisfies, M];
      | .inr $ .inr x => simp [Satisfies, M];
    . tauto;

instance : Modal.D âª± Modal.S := by
  constructor;
  . apply weakerThan_iff.mpr;
    intro Ï† hÏ†;
    induction hÏ† using D.rec' with
    | mem_GL h => exact WeakerThan.pbl h;
    | mdp ihÏ†Ïˆ ihÏ† => exact ihÏ†Ïˆ â¨€ ihÏ†;
    | _ => exact axiomT!;
  . apply Entailment.not_weakerThan_iff.mpr;
    use Axioms.T (.atom 0);
    constructor;
    . simp;
    . exact D.unprovable_T;

end

end

end LO.Modal
end
